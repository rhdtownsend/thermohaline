#:include 'forum.inc'

program pert_expand

   ! Uses

   use forum_m, split_ => split
   use flags_m
   use params_m
   use finger_m
   use linalg_m
   use pert_expand_m
   use matrix_m
   
   use ISO_FORTRAN_ENV
   use IEEE_ARITHMETIC
   use f95_lapack

   ! No implicit typing

   implicit none

   ! Variables

   character(:), allocatable :: nml_file

   integer                  :: unit
   real(RD)                 :: R_0
   real(RD)                 :: Pr
   real(RD)                 :: tau
   real(RD)                 :: H_B
   real(RD)                 :: D_B
   integer                  :: N
   character(256)           :: out_file

   type(flags_t)            :: fl
   real(RD)                 :: lam_f
   real(RD)                 :: l_f
   type(params_t)           :: pa
   integer                  :: D
   real(RD), allocatable    :: A00(:,:)
   real(RD), allocatable    :: A01(:,:)
   real(RD), allocatable    :: A10(:,:)
   real(RD), allocatable    :: A11(:,:)
   real(RD), allocatable    :: A02(:,:)
   real(RD), allocatable    :: A20(:,:)
   complex(RD), allocatable :: sig00(:)
   complex(RD), allocatable :: XL(:,:)
   complex(RD), allocatable :: XR(:,:)
   integer                  :: m
   integer                  :: i
   integer                  :: j
!   complex(RD), allocatable :: X_chk(:,:)
   complex(RD), allocatable :: V01(:,:)
   complex(RD), allocatable :: V10(:,:)
   complex(RD), allocatable :: V11(:,:)
   complex(RD), allocatable :: V02(:,:)
   complex(RD), allocatable :: V20(:,:)
   complex(RD), allocatable :: sig01(:)
   complex(RD), allocatable :: sig10(:)
   complex(RD), allocatable :: sig11(:)
   complex(RD), allocatable :: sig02(:)
   complex(RD), allocatable :: sig20(:)
   complex(RD)              :: T11
   complex(RD)              :: T02
   complex(RD)              :: T20
   type(hdf5io_t)           :: hdf5io

   namelist /params/ &
      R_0, Pr, tau, H_B, D_B, &
      N, out_file

   ! Read arguments

   @:ASSERT(n_arg() == 1,'Syntax: pert_expand nml_file')

   call get_arg(1, nml_file)

   ! Read namelist parameters

   R_0 = 1.
   Pr = 1.
   tau = 1.
   Pr = 1.
   
   H_B = 1.
   D_B = 1.

   N = 17

   open(NEWUNIT=unit, STATUS='OLD', FILE=nml_file)
   read(unit, NML=params)
   close(unit)

   print *,'R_0   :', R_0
   print *,'Pr    :', Pr
   print *,'tau   :', tau
   print *,'H_B   :', H_B
   print *,'D_B   :', D_B
   print *,'N     :', N

   fl = flags_t()

   ! Solve for fingering-instability parameters

   call solve_finger(R_0, Pr, tau, lam_f, l_f)

   print *,'lam_f :', lam_f
   print *,'l_f   :', l_f

   ! Build the A matrices

   pa = params_t(R_0, Pr, tau, H_B, D_B, 0._RD, 0._RD, N)

   pa%lam_f = lam_f
   pa%l_f = l_f

   call get_matrix_dim(N, fl, D)

   allocate(A00(D,D))
   allocate(A01(D,D))
   allocate(A10(D,D))
   allocate(A11(D,D))
   allocate(A02(D,D))
   allocate(A20(D,D))

   call eval_A00_matrix(pa, A00)
   call eval_A01_matrix(pa, A01)
   call eval_A10_matrix(pa, A10)
   call eval_A11_matrix(pa, A11)
   call eval_A02_matrix(pa, A02)
   call eval_A20_matrix(pa, A20)

   call check_pert(pa, fl, A00, A01, A10, A11, A02, A20)

   ! Diagonalize A00

   allocate(sig00(D))

   allocate(XL(D,D))
   allocate(XR(D,D))

   XL = 0
   XR = 0

   i = 1

   block_loop : do m = -N, N

      associate(A => A00(i:i+3,i:i+3), sig => sig00(i:i+3), XL_ => XL(i:i+3,i:i+3), XR_ => XR(i:i+3,i:i+3))
         call solve_eigen(A, sig, XL_, XR_)
      end associate

      i = i + 4

   end do block_loop

   ! Sanity check

!   X_chk = MATMUL(CONJG(TRANSPOSE(XL)), XR)

!   do i = 1, d
!      print 100,ABS(X_chk(i,:))
!100   format(999F6.2)
!   end do

   ! Now evaluate the V matrices

   V01 = MATMUL(CONJG(TRANSPOSE(XL)), MATMUL(A01, XR))
   V10 = MATMUL(CONJG(TRANSPOSE(XL)), MATMUL(A10, XR))
   V11 = MATMUL(CONJG(TRANSPOSE(XL)), MATMUL(A11, XR))
   V02 = MATMUL(CONJG(TRANSPOSE(XL)), MATMUL(A02, XR))
   V20 = MATMUL(CONJG(TRANSPOSE(XL)), MATMUL(A20, XR))

   ! Evaluate the sigma terms

   allocate(sig01(D))
   allocate(sig10(D))
   allocate(sig11(D))
   allocate(sig02(D))
   allocate(sig20(D))

   do i = 1, D

      sig01(i) = V01(i,i)

      sig10(i) = V10(i,i)

      sig11(i) = V11(i,i)
      sig02(i) = V02(i,i)
      sig20(i) = V20(i,i)

      do j = 1, D

         if (j /= i) then

            T11 = V01(i,j)*V10(j,i) + V10(i,j)*V01(j,i)
            T02 = V01(i,j)*V01(j,i)
            T20 = V10(i,j)*V10(j,i)

            if (T11 /= 0._RD) then
               if (sig00(i) /= sig00(j)) then
                  sig11(i) = sig11(i) + T11/(sig00(i) - sig00(j))
               else
                  sig11(i) = IEEE_VALUE(0._RD, IEEE_POSITIVE_INF)
               end if
            end if

            if (T02 /= 0._RD) then
               if (sig00(i) /= sig00(j)) then
                  sig02(i) = sig02(i) + T02/(sig00(i) - sig00(j))
               else
                  sig02(i) = IEEE_VALUE(0._RD, IEEE_POSITIVE_INF)
               end if
            end if

            if (T20 /= 0._RD) then
               if (sig00(i) /= sig00(j)) then
                  sig20(i) = sig20(i) + T20/(sig00(i) - sig00(j))
               else
                  sig20(i) = IEEE_VALUE(0._RD, IEEE_POSITIVE_INF)
               end if
            end if

         end if

      end do

   end do

   ! Write out results

   hdf5io = hdf5io_t(out_file, CREATE_FILE)

   call hdf5io%write_attr('Pr', Pr)
   call hdf5io%write_attr('tau', tau)
   call hdf5io%write_attr('R_0', R_0)
   call hdf5io%write_attr('H_B', H_B)
   call hdf5io%write_attr('D_B', D_B)
   call hdf5io%write_attr('lam_f', lam_f)
   call hdf5io%write_attr('l_f', l_f)
   call hdf5io%write_attr('N', N)

   call hdf5io%write_dset('sig00', sig00)
   call hdf5io%write_dset('sig01', sig01)
   call hdf5io%write_dset('sig10', sig10)
   call hdf5io%write_dset('sig11', sig11)
   call hdf5io%write_dset('sig02', sig02)
   call hdf5io%write_dset('sig20', sig20)

   call hdf5io%final()

   ! Finish

contains

   subroutine check_pert(pa, fl, A00, A01, A10, A11, A02, A20)

      type(params_t), intent(in) :: pa
      type(flags_t), intent(in)  :: fl
      real(RD), intent(in)       :: A00(:,:)
      real(RD), intent(in)       :: A01(:,:)
      real(RD), intent(in)       :: A10(:,:)
      real(RD), intent(in)       :: A11(:,:)
      real(RD), intent(in)       :: A02(:,:)
      real(RD), intent(in)       :: A20(:,:)

      real(RD)       :: w_f
      real(RD)       :: k_z
      real(RD)       :: q_f
      type(params_t) :: pa_
      real(RD)       :: A(SIZE(A00, 1), SIZE(A00, 2))
      real(RD)       :: A_full(SIZE(A00, 1), SIZE(A00, 2))
      integer        :: i
      integer        :: m

      integer  :: j,k
      logical  :: flag
      real(RD) :: err

      ! Set params

      w_f = 1E-1_RD
      k_z = 1E-6_RD
      q_f = w_f*k_z

      print *,q_f/2

      pa_ = params_t(pa%R_0, pa%Pr, pa%tau, pa%H_B, pa%D_B, w_f, k_z, pa%N)

      ! Build the full matrices

      call eval_matrix(pa_, fl, A_full)

      ! Compare against the expanded matrices

      A = A00 + k_z*A10 + q_f*A01 + k_z*q_f*A11 + k_z**2*A20 + q_f**2*A02

      i = 1

      block_loop: do m = -pa%N, pa%N

         if (m > -pa%N) then

            print *, 'm=',m,'(left)'

            associate(B => A(i:i+3,i-4:i-1), B_full => A_full(i:i+3,i-4:i-1))

               do j = 1,4
                  do k = 1,4

                     flag = .FALSE.

                     if (B(j,k) == 0._RD .AND. B_full(j,k) /= 0._RD) then
                        flag = .TRUE.
                     elseif (B(j,k) /= 0._RD .AND. B_full(j,k) == 0._RD) then
                        flag = .TRUE.
                     elseif (B(j,k) /= 0._RD .AND. B_full(j,k) /= 0._RD) then
                        err = (B(j,k) - B_full(j,k))/B_full(j,k)
                        if (ABS(err) > 100*EPSILON(0._RD)) then
                           flag = .TRUE.
                        end if
                     end if

                     if (flag) print *,'flag:', j,k,B(j,k), B_full(j,k)

                  end do
               end do

               !print *,'Row 1:', B(1,:), '     ', B_full(1,:)
               !print *,'Row 2:', B(2,:), '     ', B_full(2,:)
               !print *,'Row 3:', B(3,:), '     ', B_full(3,:)
               !print *,'Row 4:', B(4,:), '     ', B_full(4,:)

            end associate

         end if

         print *, 'm=',m,'(right)'

         if (m < pa%N) then

            associate(B => A(i:i+3,i+4:i+7), B_full => A_full(i:i+3,i+4:i+7))

               do j = 1,4
                  do k = 1,4

                     flag = .FALSE.

                     if (B(j,k) == 0._RD .AND. B_full(j,k) /= 0._RD) then
                        flag = .TRUE.
                     elseif (B(j,k) /= 0._RD .AND. B_full(j,k) == 0._RD) then
                        flag = .TRUE.
                     elseif (B(j,k) /= 0._RD .AND. B_full(j,k) /= 0._RD) then
                        err = (B(j,k) - B_full(j,k))/B_full(j,k)
                        if (ABS(err) > 100*EPSILON(0._RD)) then
                           flag = .TRUE.
                        end if
                     end if

                     if (flag) print *,'flag:', j,k, B(j,k), B_full(j,k)

                  end do
               end do

               !print *,'Row 1:', B(1,:), '     ', B_full(1,:)
               !print *,'Row 2:', B(2,:), '     ', B_full(2,:)
               !print *,'Row 3:', B(3,:), '     ', B_full(3,:)
               !print *,'Row 4:', B(4,:), '     ', B_full(4,:)

            end associate

         end if

         print *, 'm=',m,'(diagonal)'

         associate(B => A(i:i+3,i:i+3), B_full => A_full(i:i+3,i:i+3))

            do j = 1,4
               do k = 1,4

                  flag = .FALSE.

                  if (B(j,k) == 0._RD .AND. B_full(j,k) /= 0._RD) then
                     flag = .TRUE.
                  elseif (B(j,k) /= 0._RD .AND. B_full(j,k) == 0._RD) then
                     flag = .TRUE.
                  elseif (B(j,k) /= 0._RD .AND. B_full(j,k) /= 0._RD) then
                     err = (B(j,k) - B_full(j,k))/B_full(j,k)
                     if (ABS(err) > 100*EPSILON(0._RD)) then
                        flag = .TRUE.
                     end if
                  end if

                  if (flag) print *,'flag:', j,k,B(j,k), B_full(j,k)

               end do
            end do

            !print *,'Row 1:', B(1,:), '     ', B_full(1,:)
            !print *,'Row 2:', B(2,:), '     ', B_full(2,:)
            !print *,'Row 3:', B(3,:), '     ', B_full(3,:)
            !print *,'Row 4:', B(4,:), '     ', B_full(4,:)

         end associate

         i = i + 4

      end do block_loop

      return

   end subroutine check_pert

end program pert_expand
